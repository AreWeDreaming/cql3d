      SUBROUTINE ADCRREC(PTE, PNE)
C
C*****************************************************************************
C
C     COMPUTE RADIATIVE RECOMBINATION RATES RRAREC(JQ) FOR A GIVEN
C     ELECTRON TEMPERATURE PTE (KEV), WHERE JQ = (IONIC CHARGE + 1).
C     THESE RATES HAVE THE UNITS (SEC-1).
C     ALSO COMPUTED ARE THE RADIATION RATES ASSOCIATED WITH
C     THIS PROCESS, RADRRC(JQ).  THESE RATES ARE IN JOULES/SEC.
C
C     NOTES:
C
C     1) ELECTRONS ARE ASSUMED TO FILL IONIC SHELLS IN SIMPLE ASCENDING
C        ORDER.  THIS IS ASSUMED BOTH EXPLICITLY (THE APN ARRAY)
C        AS WELL AS IMPLICITLY THROUGH THE USE OF NVALNC VALUES
C        IN LOOP INDICES, ETC.
C     2) THROUGH USE OF THE FUNCTION EXPUND, EXP(X) IS SET EQUAL TO
C        ZERO IN THE RATE EQUATIONS FOR VALUES OF X < -45.
C     3) RATES FOR THE SPECIES JQ REFER TO TRANSFORMATIONS OF
C        THAT SPECIES INTO ADJACENT IONIZATION STATES, NOT THE
C        RATES AT WHICH THAT SPECIES IS FORMED.
C     4) UPON EVALUATION OF THE FORMULAE IT WILL BE FOUND THAT THE
C        FORMALISM USED IN XSNQ/LOKKE AND GRASBERGER (REFS 6,2)
C        IN FACT LEADS TO THE SAME EQUATIONS GIVEN IN
C        PPPL-1352/SEATON (REFS 1,3) IF THE SHIELDED CHARGE FOR
C        LEVEL N IS USED FOR 'Q'.
C
C
C     PRINCIPAL PHYSICS REFERENCES:
C
C     1) POST, JENSEN, TARTER, GRASBERGER, LOKKE, 'STEADY STATE
C        RADIATIVE COOLING RATES FOR LOW-DENSITY HIGH-TEMPERATURE
C        PLASMAS', CORRECTED PPPL-1352 (SUB. TO N.F.)
C     2) LOKKE AND GRASBERGER, 'XSNQ-U A NON-LTE EMISSION AND
C        ABSORPTION COEFFICIENT SUBROUTINE', LLL REPORT UCRL-52276
C     3) SEATON, 'RADIATIVE RECOMBINATION OF HYDROGENIC IONS',
C        MNRAS:119,81 (1959)
C     4) COX AND TUCKER, 'IONIZATION EQUILIBRIUM AND RADIATIVE
C        COOLING OF A LOW-DENSITY PLASMA', AP.J.:157,1157 (1969)
C     5) GRASBERGER, XSNQ - TOKAMAK MEMORANDUM #1, 2/11/76
C     6) XSNQ FORTRAN LISTING
C
C********************************************************************************
C
C
      IMPLICIT REAL*8 (A-H,O-Z)
      IMPLICIT INTEGER (I-N)
C
      COMMON / ADSDAT / NUCZ, NSPC, APN(100,10), AQN(100,10),
     &                  NVALNC(100), SIGMA(10,10), QN(100,10),
     &                  EN(100,10), EIN(100,5), ENM(100,5,10),
     &                  FNM(100,5,10), FNN(100), ENN(100)
C
      COMMON / ADELEC / RCLION(100), RRAREC(100), RDIREC(100),
     &                  CIZLOS(100), RADRRC(100), RADDRC(100),
     &                  RADCLX(100), RADBRM(100), TOTREC(100),
     &                  TOTRAD(100)
C
      EXTERNAL ADCEXPE1
C
      ZJLKEV = 1.6021E-16
C
C
C
      ZTE = PTE
C
      RRAREC(1) = 0.
      RADRRC(1) = 0.
C
C     ********** LOOP FOR ALL NON-NEUTRAL SPECIES **********
C
      DO 100 JQ = 2 , NSPC
C
      IVALNC = NVALNC(JQ)
      IVJQM1 = NVALNC(JQ - 1)
C
C
C
C     CALCULATE RADIATIVE RECOMBINATION RATES PER SHELL AND SUM THEM
C     (SUMS (FRACTIONAL VACANCY OF SHELL) * (ALPHAN) ; ALPHAN
C     FROM CORRECTED PPPL-1352).  ALSO SUM CONTINUUM AND LINE
C     RADIATION RATES DUE TO RADIATIVE RECOMBINATIONS TO EACH SHELL.
C
C
C
      ZRSUM = 0.
      ZRSUMC = 0.
      ZRSUML = 0.
C
      DO 31 JN = IVALNC,10
C
C     SKIP VALANCE SHELL IF FULL TO AVOID UNDEFINED EN, QN
C
      IF(AQN(JQ,JN) .EQ. 0.0) GO TO 31
C
      ZXN = EN(JQ,JN)/ZTE
C
      ZRTEMP = AQN(JQ,JN) * QN(JQ,JN) * (ZXN**1.5) * ADCEXPE1(ZXN)
C
C     HIGH-LYING LEVELS CORRECTION FOR NLVLMX=10 IS 5.13
C
      IF(JN .EQ. 10) ZRTEMP = ZRTEMP * 5.13
      ZRSUM = ZRSUM + ZRTEMP
C
      ZRTMPL = ENM(JQ-1,IVJQM1,JN) * ZRTEMP
      ZRSUML = ZRSUML + ZRTMPL
C
      ZRTMPC = AQN(JQ,JN) * QN(JQ,JN) * DSQRT(ZXN) * EN(JQ,JN)
      ZRSUMC = ZRSUMC + ZRTMPC
   31 CONTINUE
C
      RRAREC(JQ) = PNE * 5.20E-14 * ZRSUM
      IF(ZRSUMC .LT. 1.E-07) ZRSUMC = 0.
      ZRSUMC = 8.32E-30 * ZRSUMC
C
      IF(ZRSUML .LT. 1.E-7) ZRSUML = 0.
      ZRSUML = ZJLKEV * 5.20E-14 * ZRSUML
      RADRRC(JQ) = PNE * (ZRSUMC + ZRSUML)
C
C
  100 CONTINUE
C
C
      RETURN
      END
