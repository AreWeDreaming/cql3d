      SUBROUTINE ADCDREC(PTE, PNE)
C
C************************************************************************
C
C     COMPUTES DIELECTRONIC RECOMBINATION RATE RDIREC(JQ)
C     FOR A GIVEN ELECTRON TEMPERATURE PTE (KEV) AND ELECTRON
C     DENSITY PNE (CM-3), WHERE JQ = (IONIC CHARGE +1).
C     THESE RATES HAVE THE UNITS (SEC-1).  SINCE
C     THE RATE COEFF DEPEND ON NE, THE TOTAL RECOMBINATION RATE
C     PER ION IS NOT STRICTLY A LINEAR FUNCTION OF NE.  HOWEVER,
C     THE DIELECTRONIC COLLISIONAL-INTERRUPTION CORRECTION IS
C     ONLY WEAKLY DEPENDENT ON THE DENSITY, AND THUS THE NON-LINEARITY
C     IS GENERALLY SMALL.
C
C     ALSO COMPUTED ARE THE RADIATION RATES ASSOCIATED WITH THIS
C     PROCESS, RADDRC(JQ), WHICH INCLUDE CONTRIBUTIONS FROM
C     THE RADIATIVE DECAYS OF BOTH THE EXCITED AND CAPTURED ELECTRONS.
C     THESE RATES ARE IN JOULES/SEC.
C
C     NOTES:
C
C     1) ELECTRONS ARE ASSUMED TO FILL IONIC SHELLS IN SIMPLE ASCENDING
C        ORDER.  THIS IS ASSUMED BOTH EXPLICITLY (THE APN ARRAY)
C        AS WELL AS IMPLICITLY THROUGH THE USE OF NVALNC VALUES
C        IN LOOP INDICES, ETC.
C     2) THROUGH USE OF THE FUNCTION EXPUND, EXP(X) IS SET EQUAL TO
C        ZERO IN THE RATE EQUATIONS FOR VALUES OF X < -45.
C     3) RATES FOR THE SPECIES JQ REFER TO TRANSFORMATIONS OF
C        THAT SPECIES INTO ADJACENT IONIZATION STATES, NOT THE
C        RATES AT WHICH THAT SPECIES IS FORMED.
C
C
C     PRINCIPAL PHYSICS REFERENCES:
C
C     1) POST, JENSEN, TARTER, GRASBERGER, LOKKE, 'STEADY STATE
C        RADIATIVE COOLING RATES FOR LOW-DENSITY HIGH-TEMPERATURE
C        PLASMAS', CORRECTED PPPL-1352 (SUB. TO N.F.)
C     2) BURGESS, 'A GENERAL FORMULA FOR THE ESTIMATION OF
C        DIELECTRONIC RECOMBINATION COEFFICIENTS IN LOW-DENSITY
C        PLASMAS', AP.J. 141, 1588 (1965).
C     3) GRASBERGER, XSNQ - TOKAMAK MEMORANDUM #1, 2/11/76
C     4) GRASBERGER, XSNQ - TOKAMAK MEMORANDUM #4, 7/29/76
C     5) GRASBERGER, XSNQ - TOKAMAK MEMORANDUM #10, 1/5/77
C     6) MERTS, COWAN, MAGEE, 'THE CALCULATED POWER OUTPUT
C        FROM A THIN IRON-SEEDED PLASMA', LOS ALAMOS REPORT LA-6220-MS
C     7) XSNQ FORTRAN LISTING
C     8) ANSARI, ELWERT AND MUCKLICH, 'ON DIELECTRONIC RECOMBINATION',
C        ZEITSCHRIFT NATURFORSCHUNG 25A, 1781 (1970).
C
C***************************************************************************
C
C
      IMPLICIT REAL*8 (A-H,O-Z)
      IMPLICIT INTEGER (I-N)
C
      COMMON / ADSDAT / NUCZ, NSPC, APN(100,10), AQN(100,10),
     &                  NVALNC(100), SIGMA(10,10), QN(100,10),
     &                  EN(100,10), EIN(100,5), ENM(100,5,10),
     &                  FNM(100,5,10), FNN(100), ENN(100)
C
      COMMON / ADELEC / RCLION(100), RRAREC(100), RDIREC(100),
     &                  CIZLOS(100), RADRRC(100), RADDRC(100),
     &                  RADCLX(100), RADBRM(100), TOTREC(100),
     &                  TOTRAD(100)
C
      COMMON /ADDIEL/ CDNN(100), CDNM(100), YHNNA, YHNNB,
     &                YHNNC, YHNMA, YHNMB, YHNMC,
     &                LDRMLT

C
      EXTERNAL ADCEUND
C
      ESML=-45.d0
      ZJLKEV = 1.6021d-16
C
C     CALL ADCDRCC TO FILL CDNN AND CDNM MULTIPICATIVE CONSTANT ARRAYS
C
      CALL ADCDRC
C
      ZTE = PTE
      ZNE = PNE
C
      ZNUC = DBLE(NUCZ)
C
      RDIREC(1) = 0.
      RADDRC(1) = 0.
      RDIREC(NSPC) = 0.
      RADDRC(NSPC) = 0.
C
      IF(NUCZ .EQ. 1) RETURN
C
C
C     ********** LOOP FOR ALL SPECIES EXCEPT NEUTRAL **********
C              ********** AND FULLY IONIZED **********
C
      DO 100 JQ = 2 , NUCZ
C
C
      ZCHG = DBLE(JQ-1)
      IVALNC = NVALNC(JQ)
C
C
C
C     ************* DIELECTRONIC RECOMBINATION RATE *************
C
C     (EQUATIONS AND VARIABLES FOLLOW PPPL-1352/BURGESS FORMALISM)
C
C
C
      ZQ = ZCHG
      ZQP1 = ZCHG + 1
C
      ZBQ = DSQRT(  (ZQ * (ZQ+1.)**5) / (ZQ*ZQ + 13.4)  )
      ZNT = (4.77E+18 * (ZQ**6) * DSQRT(ZTE) / ZNE)**0.142857143
      ZDNN = .0068 * ZNT * (1.+.001*ZNT) / (1.+.00002*ZNT*ZNT)
      ZZZ = .0015 * ( (ZQ+1.) * ZNT )**2
      ZDNM = ZZZ / (1. + ZZZ)
      ZZZNN = 7.59E-14 * ZBQ * ZDNN / (ZTE**1.5)
      ZZZNM = 7.59E-14 * ZBQ * ZDNM / (ZTE**1.5)
      ZZZA = 1. + .015 * (ZQ**3) / ( (ZQ+1.)**2 )
      ZZZET = ( (ZQ + 1.)**2 ) / (73.5 * ZZZA * ZTE)
C
C
C     COMPUTE N - N CONTRIBUTION TO SUM FIRST
C
C
      ZEIJNN = ENN(JQ) / (.0136 * ZQP1 * ZQP1)
      ZYNN = (ZQ + 1.) * ZEIJNN
      ZAYNN = DSQRT(ZYNN) / (1. + .105 * ZYNN + .015 * ZYNN**2)
C
      ZDSUM0 = FNN(JQ) * ZAYNN * ADCEUND(-ZZZET*ZEIJNN , ESML)
C
C
C     SUM OVER N - M TRANSITIONS (NOTE THAT TRANSITIONS FROM
C     INNER SHELLS TO THE VALENCE SHELL AND ABOVE ARE INCLUDED)
C
C
      ZDSUM = 0.
      ZDSUMR = 0.
C
      DO 40 JN = 1 , IVALNC
      IIJM = IVALNC
      IF(JN .EQ. IVALNC .OR.
     &          AQN(JQ,IVALNC) .EQ. 0.) IIJM = IVALNC + 1
C
      DO 40 JM = IIJM , 10
C
      ZEIJ = ENM(JQ,JN,JM) / (.0136 * ZQP1**2)
      ZY = (ZQ + 1.) * ZEIJ
      ZAY = DSQRT(ZY) / (2. + .420 * ZY + .06 * ZY**2)
C
      ZDTEMP = FNM(JQ,JN,JM) * ZAY * ADCEUND(-ZZZET*ZEIJ , ESML)
C
      ZDSUM = ZDSUM + ZDTEMP
      ZDSUMR = ZDSUMR + ENM(JQ,JN,JM) * ZDTEMP
   40 CONTINUE
C
      ZDNNRT = ZZZNN * ZDSUM0
      ZDNMRT = ZZZNM * ZDSUM
CW    WRITE(109,909)JQ,ZDNNRT,ZDNMRT
CW  909 FORMAT(1X,'JQ =',I3,3X,'ZDNNRT =',1PE12.2,'  ZDNMRT =',E12.2)
      RDIREC(JQ) = PNE * (CDNN(JQ) * ZDNNRT + CDNM(JQ) * ZDNMRT)
C
      ITEMP = NVALNC(JQ-1)
      ZIQ = EN(JQ,ITEMP)
      ZTEMP = CDNN(JQ) * (ZIQ + ENN(JQ)) * ZDNNRT +
     &              CDNM(JQ) * (ZIQ * ZDNMRT + ZZZNM * ZDSUMR)
      IF(ZTEMP .LT. 1.E-21) ZTEMP = 0.
      RADDRC(JQ) = PNE * ZJLKEV * ZTEMP
C
C
  100 CONTINUE
C
C
      RETURN
      END




      SUBROUTINE ADCDRC
C
C*************************************************************
C     SETUP THE DIELECTRONIC RECOMBINATION RATE COEFFICIENT
C     MULTIPLICATIVE CONSTANT ARRAYS CDNN AND CDNM ACCORDING
C     TO THE LDRMLT SWITCH.
C
C     LDRMLT = 0     USE CDNN, CDNM ARRAYS AS GIVEN
C     LDRMLT = 1     CDNN, CDNM ARRAYS SET TO 1.0
C     LDRMLT = 2     USE Y. HAHN FACTORS TO SETUP CDNN, CDNM
C**************************************************************
C
      IMPLICIT REAL*8 (A-H,O-Z)
      IMPLICIT INTEGER (I-N)
C
      COMMON / ADSDAT / NUCZ, NSPC, APN(100,10), AQN(100,10),
     &                  NVALNC(100), SIGMA(10,10), QN(100,10),
     &                  EN(100,10), EIN(100,5), ENM(100,5,10),
     &                  FNM(100,5,10), FNN(100), ENN(100)
C
      COMMON / ADDIEL / CDNN(100), CDNM(100),
     &                  YHNNA, YHNNB, YHNNC, YHNMA, YHNMB, YHNMC,
     &                  LDRMLT
C
C
      IF(LDRMLT .EQ. 0) RETURN
C
      DO 20 JSPC = 1, 100
      CDNN(JSPC) = 1.0
      CDNM(JSPC) = 1.0
   20 CONTINUE
C
      IF(LDRMLT .EQ. 1) RETURN
C
C
      DO 100 JSPC = 2, NUCZ
C
      ZNELEC = DBLE(NUCZ - JSPC + 1)
C
      CDNN(JSPC) = YHNNA * DEXP(-YHNNB*DSQRT(ZNELEC) ) *
     &                  (DBLE(NUCZ)**YHNNC)
C
      CDNM(JSPC) = YHNMA * DEXP(-YHNMB*DSQRT(ZNELEC)) *
     &                  (DBLE(NUCZ)**YHNMC)
  100 CONTINUE
C
      RETURN
      END

