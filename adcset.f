        SUBROUTINE ADCSET(KNUCZ)
C
C*************************************************************
C     SETUP BASIC ATOMIC STRUCTURE DATA FOR CHOSEN ELEMENT
C     (SHELL POPULATIONS, ENERGIES, ETC.)
C*************************************************************
C
      IMPLICIT REAL*8 (A-H,O-Z)
      IMPLICIT INTEGER (I-N)
C
      COMMON / ADSDAT / NUCZ, NSPC, APN(100,10), AQN(100,10),
     &                  NVALNC(100), SIGMA(10,10), QN(100,10),
     &                  EN(100,10), EIN(100,5), ENM(100,5,10),
     &                  FNM(100,5,10), FNN(100), ENN(100)
C
      COMMON / ADPARM / LADEN, LADTIP, LTIPOK, LECI, LECIOK
C
      EXTERNAL ADCAPN,ADCMAYR,ADCMORE,ADCTIP,ADCFNM
C
      NUCZ = KNUCZ
C
      CALL ADCAPN
C
      IF(LADEN .NE. 0 .AND. LADEN .NE. 1) STOP
      IF(LADEN .EQ. 0) CALL ADCMAYR
      IF(LADEN .EQ. 1) CALL ADCMORE
C
      IF(LADTIP .NE. 0 .AND. LADTIP .NE. 1) STOP
      LTIPOK = 0
      IF(LADTIP .EQ. 1) CALL ADCTIP(LTIPOK)
C
      CALL ADCFNM
C
      RETURN
      END



      SUBROUTINE ADCAPN
C
C
C******************************************************************
C     INPUT : NUCZ
C     OUTPUT : NSPC, APN, AQN, NVALNC
C
C     ASSIGN THE NUMBER OF ELECTRONS PER SHELL FOR EACH
C     IONIC SPECIES.  APN(JQ,JN) = NUMBER OF ELECTRONS IN
C     SHELL JN FOR SPECIES WITH CHARGE JQ-1, WHILE AQN(JQ,JN) =
C     (2.*JN**2 - APN(JQ,JN))/(2.*JN**2) IS THE FRACTIONAL VACANCY.
C     THE SHELLS ARE FILLED IN SIMPLE CONSECUTIVE ASCENDING ORDER.
C     ALSO, THE PRINCIPAL QUANTUM NUMBER OF THE HIGHEST
C     OCCUPIED ('VALENCE') SHELL FOR EACH SPECIES IS STORED
C     IN NVALNC(JQ).
C
C     NOTE:  FOR THE FULLY IONIZED SPECIES NVALNC IS DEFAULTED TO 1.
C     ALSO: NSPC = NUCZ + 1 IS SET BY THIS ROUTINE
C********************************************************************
C
      IMPLICIT REAL*8 (A-H,O-Z)
      IMPLICIT INTEGER (I-N)
C
      COMMON / ADSDAT / NUCZ, NSPC, APN(100,10), AQN(100,10),
     &                  NVALNC(100), SIGMA(10,10), QN(100,10),
     &                  EN(100,10), EIN(100,5), ENM(100,5,10),
     &                  FNM(100,5,10), FNN(100), ENN(100)
C
C     SET MAXIMUM SPECIES INDEX NSPC = NUCZ + 1
C
      NSPC = NUCZ +1
C
C
      DO 1 JQ = 1,NSPC
      ICOUNT = NUCZ-JQ+1
C
C
      DO 2 JN = 1,10
      IFULL = 2*JN*JN
      ISHELL = MIN0 (ICOUNT,IFULL)
      ICOUNT = ICOUNT - ISHELL
      IF(ISHELL.GT.0) NVALNC(JQ) = JN
      APN(JQ,JN) = DBLE(ISHELL)
      AQN(JQ,JN) = DBLE(IFULL - ISHELL)/DBLE(IFULL)
C
CW    WRITE(10,900)JQ,JN,APN(JQ,JN),NVALNC(JQ)
CW  900 FORMAT(1X,'JQ =',I3,' JN =',I3,'  APN=',F4.0,'  NVALNC=',I3)
C
    2 CONTINUE
C
C
    1 CONTINUE
C
C
      NVALNC(NSPC) = 1
C
      RETURN
      END



      SUBROUTINE ADCMORE
C
C******************************************************************
C     INPUT : NUCZ, NSPC, APN, NVALNC
C     OUTPUT : EN, EIN, ENM, QN
C
C     EXCITATION ENERGIES AND IONIZATION POTENTIALS ARE
C     TABULATED FOR ALL OCCUPIED SHELLS FOR EACH SPECIES.  ENERGIES
C     ARE IN KEV, JQ = SPECIES INDEX = IONIC CHARGE + 1, AND
C     JN = SHELL INDEX.
C
C     USES THE R. MORE REVISED SHIELDING CONSTANTS (AND NEW
C     CALCULATION PROCEDURE) IN LIEU OF THE ORIGINAL 'XSNQ'
C     TREATMENT BASED ON THE H. MAYER SHIELDING CONSTANTS.
C     (ORIGINAL TREATMENT, REFS. 1-4; PRESENT TREATMENT, REF. 5).
C     CERTAIN ASPECTS OF THE FORMALISM (E.G. QN AND EN ARRAYS) ARE
C     'HISTORICAL' IN NATURE.
C
C
C          SIGMA(JN,JM)   R. MORE SHIELDING CONSTANTS
C
C          QN(JQ,JN)      EFFECTIVE NUCLEAR CHARGE AT SHELL JN
C                         CONSIDERING ALL ELECTRONS IN THE GROUND STATE
C                         ION, NOW COMPUTED FROM ZSEN (RATHER THAN
C                         THAN VICE-VERSA).  THIS IS *NOT* THE
C                         SAME AS QN IN MORE'S NOTATION, WHICH
C                         IS THE SHIELDED CHARGE DUE TO INNER
C                         SHIELDING ONLY.
C
C          EN(JQ,JN)      ENERGY LEVELS FOR RECOMBINATION OF THE
C                         GROUND STATE ION.
C
C          EIN(JQ,JN)     IONIZATION POTENTIAL FOR ELECTRON IN SHELL
C                         JN CALCULATED FROM THE DIFFERENCE IN TOTAL
C                         ION ENERGIES WITH AND WITHOUT AN ELECTRON
C                         REMOVED FROM SHELL JN.
C
C          ENM(JQ,JN,JM)  EXCITATION ENERGY FROM SHELL JN UP TO SHELL
C                         JM CALULATED FROM THE DIFFERENCE IN
C                         THE TOTAL ION ENERGIES OF OF THE GROUND
C                         STATE AND EXCITED STATE ELECTRON POPULATIONS.
C
C          ZBETOT(JQ)     TOTAL BINDING ENERGY FOR EACH ION.
C
C
C     REFERENCES:
C
C     1) POST ET AL., PPPL-1352 (CORRECTED)
C     2) LOKKE AND GRASBERGER, LLL REPORT UCRL-52276 (CORRECTED)
C     3) H. MAYER, 'METHODS OF OPACITY CALCULATIONS', LOS ALAMOS
C        SCIENTIFIC  LABORATORY LA-647 (1947).
C     4) XSNQ LISTING
C     5) R. MORE 'ATOMIC PHYSICS IN INERTIAL CONFINEMENT FUSION -
C        PART I' LLL REPORT UCRL-84991 (1981) (TO APPEAR
C        IN 'APPLIED ATOMIC COLLISION PHYSICS', VOLUME II,
C        ACADEMIC PRESS)
C
C     NOTE: THERE ARE DISCREPANCIES BETWEEN THESE REFERENCES FOR A FEW
C           OF THE H. MAYER SHIELDING CONSTANT VALUES.
C**************************************************************************
C
      IMPLICIT REAL*8 (A-H,O-Z)
      IMPLICIT INTEGER (I-N)
C
      DIMENSION ZSEN(10), ZBETOT(100), ZDUM(10)
C
      COMMON / ADSDAT / NUCZ, NSPC, APN(100,10), AQN(100,10),
     &                  NVALNC(100), SIGMA(10,10), QN(100,10),
     &                  EN(100,10), EIN(100,5), ENM(100,5,10),
     &                  FNM(100,5,10), FNN(100), ENN(100)
C
      EXTERNAL ADCSHEN
C
C
C     MAIN SPECIES LOOP
C
      DO 100 JQ = 1, NSPC
      JJQ = JQ
      IVALNC = NVALNC(JQ)
C
C     COMPUTE ZBETOT AND ZSEN
C
      IHOLE = 0
      IEXCIT = 0
      CALL ADCSHEN(JJQ, IHOLE, IEXCIT, ZSEN(1), ZBETOT(JJQ))
C
C     COMPUTE EIN, ENM, AND EN FROM DIFFERENCES IN TOTAL
C     ION ENERGIES (RATHER FROM ZSEN'S) IN ORDER TO AGREE
C     WITH RESULTS IN MORE'S FIGURE III-7.  COMPUTE
C     QN FROM THE ZSEN IN ORDER TO HAVE THE APPROPRIATE LIMITING
C     INTEGRAL CHARGES AT HIGH N SHELLS.
C
C     CLEAR ARRAYS
C
      DO 30 JN = 1, 5
C
      EIN(JQ,JN) = 0.0
C
      DO 20 JM = 1, 10
      ENM(JQ,JN,JM) = 0.0
   20 CONTINUE
   30 CONTINUE
C
C     SKIP FULLY STRIPPED STATE
C
      IF(JQ. EQ. NSPC) GO TO 60
C
      DO 50 JN = 1, IVALNC
C
      IHOLE = JN
      IEXCIT = 0
      CALL ADCSHEN(JJQ, IHOLE, IEXCIT, ZDUM(1), ZBEIZ)
      EIN(JQ,JN) = ZBETOT(JQ) - ZBEIZ
C
      IIJM = IVALNC
      IF(JN .EQ. IVALNC .OR. AQN(JQ,IVALNC) .EQ. 0.0) IIJM = IIJM+1
C
      DO 40 JM = IIJM, 10
      IHOLE = JN
      IEXCIT = JM
      CALL ADCSHEN(JJQ, IHOLE, IEXCIT, ZDUM(1), ZBEEX)
      ENM(JQ,JN,JM) = ZBETOT(JQ) - ZBEEX
   40 CONTINUE
   50 CONTINUE
C
C     CLEAR ARRAYS
C
   60 DO 70 JN = 1, 10
      EN(JQ,JN) = 0.0
      QN(JQ,JN) = 0.0
   70 CONTINUE
C
C     SKIP NEUTRAL STATE
C
      IF(JQ .EQ. 1) GO TO 100
C
C     COMPUTE EN AND QN
C
      IIJN = IVALNC
      IF(AQN(JQ,IVALNC) .EQ. 0.0) IIJN = IVALNC+1
C
      DO 80 JN = IIJN, 10
      IHOLE = 0
      IEXCIT = JN
      CALL ADCSHEN(JJQ, IHOLE, IEXCIT, ZDUM(1), ZBEREC)
      EN(JQ,JN) = ZBEREC - ZBETOT(JQ)
      QN(JQ,JN) = DBLE(JN) * DSQRT(ZSEN(JN)/0.0136)
   80 CONTINUE

  100 CONTINUE
C
      RETURN
      END



      SUBROUTINE ADCSHEN(KQ, KHOLE, KEXCIT, PSEN, PBETOT)
C
C*****************************************************************
C     INPUT: NUCZ, KQ, KHOLE, KEXCIT, APN
C     OUTPUT: PSEN(10), PBETOT
C
C     COMPUTE SHELL ENERGIES USING NEW SHIELDING CONSTANTS
C     AND METHOD DUE TO R. MORE (UCRL-84991).
C
C     NUCZ = ION NUCLEAR CHARGE
C     KQ = SPECIES INDEX = IONIC CHARGE + 1
C     KHOLE, KEXCIT = ELECTRON REMOVED FROM SHELL KHOLE AND/OR
C                     ADDED (EXCITED) TO SHELL KEXCIT BEFORE
C                     ENERGIES ARE COMPUTED (KHOLE = 0 NO ELECTRON
C                     REMOVED, KEXCIT = 0 NO ELECTRON ADDED)
C     APN(100,10) = ARRAY OF SHELL POPULATIONS
C     PSEN = OUTPUT ARRAY OF SHELL ENERGIES (KEV) (DEFINED POSITIVE)
C     PBETOT = OUTPUT TOTAL ION ENERGY (DEFINED POSITIVE)
C********************************************************************
C
      IMPLICIT REAL*8 (A-H,O-Z)
      IMPLICIT INTEGER (I-N)
C
      DIMENSION PSEN(10), ZAPN(10), ZQ(10), ZEN0(10)
C
      COMMON / ADSDAT / NUCZ, NSPC, APN(100,10), AQN(100,10),
     &                  NVALNC(100), SIGMA(10,10), QN(100,10),
     &                  EN(100,10), EIN(100,5), ENM(100,5,10),
     &                  FNM(100,5,10), FNN(100), ENN(100)
C
      COMMON / SMORE / SIGMOR(10,10)
C
      DATA ((SIGMOR(JN,JM),JN=1,10),JM=1,10)/
     1 0.3125, 0.9380, 0.9840, 0.9954, 0.9970,
     1 0.9970, 0.9990, 0.9999, 0.9999, 0.9999,
     2 0.2345, 0.6038, 0.9040, 0.9722, 0.9979,
     2 0.9880, 0.9900, 0.9990, 0.9990, 0.9999,
     3 0.1093, 0.4018, 0.6800, 0.9155, 0.9796,
     3 0.9820, 0.9860, 0.9900, 0.9920, 0.9999,
     4 0.0622, 0.2430, 0.5150, 0.7100, 0.9200,
     4 0.9600, 0.9750, 0.9830, 0.9860, 0.9900,
     5 0.0399, 0.1597, 0.3527, 0.5888, 0.7320,
     5 0.8300, 0.9000, 0.9500, 0.9700, 0.9800,
     6 0.0277, 0.1098, 0.2455, 0.4267, 0.5764,
     6 0.7248, 0.8300, 0.9000, 0.9500, 0.9700,
     7 0.0204, 0.0808, 0.1811, 0.3184, 0.4592,
     7 0.6098, 0.7374, 0.8300, 0.9000, 0.9500,
     8 0.0156, 0.0624, 0.1392, 0.2457, 0.3711,
     8 0.5062, 0.6355, 0.7441, 0.8300, 0.9000,
     9 0.0123, 0.0493, 0.1102, 0.1948, 0.2994,
     9 0.4222, 0.5444, 0.6558, 0.7553, 0.8300, 0.0100, 0.0400,
     & 0.0900, 0.1584, 0.2450, 0.3492, 0.4655, 0.5760, 0.6723, 0.7612/
C
      DO 10 JN = 1, 10
      ZAPN(JN) = APN(KQ,JN)
   10 CONTINUE
C
C     REMOVE ELECTRON FROM SHELL KHOLE
C
      IF(KHOLE .GT. 0 .AND. ZAPN(KHOLE) .GT. 0.0)
     &         ZAPN(KHOLE) = ZAPN(KHOLE) - 1.0
C
C     ADD ELECTRON TO SHELL KEXCIT
C
      IF(KEXCIT .GT. 0) ZAPN(KEXCIT) = ZAPN(KEXCIT) + 1.0
C
C     COMPUTE NUCLEAR CHARGE AS SHIELDED BY INNER ELECTRONS ONLY
C
      DO 30 JN = 1, 10
C
      ZQ(JN) = DBLE(NUCZ) - 0.5 * SIGMOR(JN,JN) * ZAPN(JN)
C
      IF(JN .EQ. 1) GO TO 30
      JNM1 = JN - 1
C
      DO 20 JM = 1, JNM1
      ZQ(JN) = ZQ(JN) - SIGMOR(JN,JM) * ZAPN(JM)
   20 CONTINUE
   30 CONTINUE
C
C     COMPUTE POTENTIAL DUE TO OUTER ELECTRONS
C
      DO 50 JN = 1, 10
      ZEN0(JN) = 0.0136 * SIGMOR(JN,JN) * ZAPN(JN)
     &           * ZQ(JN) / (DBLE(JN)**2)
C
      IF(JN .EQ. 10) GO TO 50
      JNP1 = JN + 1
C
      DO 40 JM = JNP1, 10
      ZEN0(JN) = ZEN0(JN) + 0.0272 * ZAPN(JM) * SIGMOR(JM,JN)
     &                      * ZQ(JM) / (DBLE(JM)**2)
   40 CONTINUE
C
   50 CONTINUE
C
C     COMPUTE FINAL SHELL ENERGIES AND TOTAL ION BINDING ENERGY,
C     BOTH DEFINED POSITIVE.
C
      PBETOT = 0.0
C
      DO 60 JN = 1, 10
      PSEN(JN) = 0.0136 * ((ZQ(JN)/DBLE(JN))**2) - ZEN0(JN)
      PBETOT = PBETOT + 0.0136 * ZAPN(JN) * ((ZQ(JN)/DBLE(JN))**2)
   60 CONTINUE
C
      RETURN
      END



      SUBROUTINE ADCMAYR
C
C*********************************************************************
C     INPUT : NUCZ, NSPC, APN
C     OUTPUT : SIGMA, QN, EN, EIN, ENM
C
C     COMPUTES ENERGY LEVELS FOR THE GROUND STATE IONIC SPECIES
C     AND FOR SPECIES WITH SINGLE INNER ELECTRON VACANCIES.
C     EXCITATION ENERGIES AND IONIZATION POTENTIALS ARE THUS
C     TABULATED FOR ALL OCCUPIED SHELLS FOR EACH SPECIES.  ENERGIES
C     ARE IN KEV, JQ = SPECIES INDEX = IONIC CHARGE + 1, AND
C     JN = SHELL INDEX.
C
C     FOR EACH CONFIGURATION AND SHELL OF INTEREST, THE EFFECTIVE
C     NUCLEAR CHARGE IS CALCULATED USING THE H. MAYER SHIELDING
C     CONSTANTS.  THE ENERGY LEVELS ARE THEN FOUND USING THIS EFFECTIVE
C     CHARGE IN THE PAULI APPROXIMATION TO THE DIRAC ENERGY LEVEL
C     FORMULA.  THE SHELL ENERGY IS TAKEN AS THE MEAN OF THE LOWEST
C     AND HIGHEST SUBSHELL ENERGIES FOR THAT N.
C
C
C          SIGMA(JN,JM)   H. MAYER SHIELDING CONSTANTS
C
C          QN(JQ,JN)      EFFECTIVE NUCLEAR CHARGE AT SHELL JN
C                         CONSIDERING ALL ELECTRONS IN THE GROUND STATE ION.
C
C          EN(JQ,JN)      ENERGY LEVELS FOR GROUND STATE ION
C                         (FORMERLY EQN(JQ,JN) IN PREVIOUS VERSIONS)
C
C          EIN(JQ,JN)     IONIZATION POTENTIAL FOR ELECTRON IN SHELL JN;
C                         USES SHIELDED CHARGE CALCULATED WITH ONE
C                         ELECTRON REMOVED FROM SHELL JN.
C
C          ENM(JQ,JN,JM)  EXCITATION ENERGY FROM SHELL JN UP TO SHELL JM;
C                         THE DIFFERENCE BETWEEN THE JN AND JM ENERGY LEVELS
C                         CALCULATED WITH THE JN SHELL ELECTRON REMOVED.
C
C
C
C     REFERENCES:
C
C     1) POST ET AL., PPPL-1352 (CORRECTED)
C     2) LOKKE AND GRASBERGER, LLL REPORT UCRL-52276 (CORRECTED)
C     3) H. MAYER, 'METHODS OF OPACITY CALCULATIONS', LOS ALAMOS
C        SCIENTIFIC  LABORATORY LA-647 (1947).
C     4) XSNQ LISTING
C
C     NOTE: THERE ARE DISCREPANCIES BETWEEN THESE REFERENCES FOR A FEW
C           SHIELDING CONSTANT VALUES.
C******************************************************************************
C
      IMPLICIT REAL*8 (A-H,O-Z)
      IMPLICIT INTEGER (I-N)
C
      DIMENSION ZSIGMA(10,10)
C
      COMMON / ADSDAT / NUCZ, NSPC, APN(100,10), AQN(100,10),
     &                  NVALNC(100), SIGMA(10,10), QN(100,10),
     &                  EN(100,10), EIN(100,5), ENM(100,5,10),
     &                  FNM(100,5,10), FNN(100), ENN(100)
C
C
      COMMON / SMAYER / SIGMAY(10,10)
C
      EXTERNAL ADCPALI
C
      DATA ((SIGMAY(JN,JM),JN=1,10),JM=1,10)/
     1 .625,.938,.981,.987,.994,.997,.999,1.00,1.00,1.00,
     2 .235,.690,.893,.940,.970,.984,.990,.993,.995,1.00,
     3 .109,.397,.702,.850,.920,.955,.970,.980,.990,1.00,
     4 .0617,.235,.478,.705,.830,.900,.950,.970,.980,.990,
     5 .0398,.155,.331,.531,.720,.830,.900,.950,.970,.980,
     6 .0277,.109,.239,.400,.580,.735,.830,.900,.950,.970,
     7 .0204,.0808,.178,.310,.459,.610,.745,.830,.900,.950,
     8 .0156,.0625,.138,.243,.371,.506,.635,.750,.830,.900,
     9 .0123,.0494,.111,.194,.299,.431,.544,.656,.760,.830,
     & .010,.040,.090,.158,.245,.353,.460,.576,.670,.765/
C
C
C
C     START MAIN SPECIES LOOP
C
C
      DO 100 JQ = 1 , NSPC
C
      IVALNC = NVALNC(JQ)
C
C
C     COMPUTE QN AND EN
C
      DO 30 JN = 1 , 10
      ZJN = DBLE(JN)
C
      ZQN = DBLE(NUCZ)
C
      DO 20 JM = 1 , IVALNC
      ZQN = ZQN - SIGMAY(JN,JM) * APN(JQ,JM)
   20 CONTINUE
C
      ZQN = ZQN + APN(JQ,JN) * SIGMAY(JN,JN) / (2. * ZJN**2)
C
      QN(JQ,JN) = ZQN
      IN = JN
      EN(JQ,JN) = ADCPALI(ZQN,IN)
   30 CONTINUE
C
C
C     CLEAR ARRAYS THEN COMPUTE EIN AND ENM
C
C
      DO 40 JN = 1 , 5
      EIN(JQ,JN) = 0.
      DO 40 JM = 1 , 10
      ENM(JQ,JN,JM) = 0.
   40 CONTINUE
C
C     SKIP IONIZATION AND EXCITATION ENERGIES FOR FULLY STRIPPED ION
C
      IF(JQ .EQ. NSPC) GO TO 100
C
C
      DO 70 JN = 1 , IVALNC
      ZJN = DBLE(JN)
C
C     COMPUTE ZQNN = EFFECTIVE CHARGE AT SHELL JN WITH ONE ELECTRON
C     REMOVED FROM SHELL JN.
C
      ZQNN = QN(JQ,JN) + SIGMAY(JN,JN) * (1. - 1./(2.*ZJN**2))
C
C     EIN(JQ,JN) = IONIZATION POTENTIAL FOR SHELL JN
C
      IN = JN
      EIN(JQ,JN) = ADCPALI(ZQNN,IN)
C
C     COMPUTE ENM(JQ,JN,JM) FROM ZQNM, EFFECTIVE CHARGE AT SHELL
C     JM WITH ONE ELECTRON REMOVED FROM SHELL JN.
C
      IIJM = IVALNC
      IF(JN .EQ. IVALNC .OR.
     &         AQN(JQ,IVALNC) .EQ. 0.) IIJM = IVALNC + 1
C
      DO 60 JM = IIJM , 10
C
      ZQNM = QN(JQ,JM) + SIGMAY(JM,JN)
C
      IM = JM
      ENM(JQ,JN,JM) = EIN(JQ,JN) - ADCPALI(ZQNM,IM)
C
   60 CONTINUE
C
C
   70 CONTINUE
C
C
  100 CONTINUE
C
C
      RETURN
      END


      REAL*8 FUNCTION ADCPALI(PQ,KN)
C
C******************************************************************
C     EVALUATE THE PAULI APPROXIMATION TO THE
C     THE DIRAC ENERGY LEVEL EQUATION, TAKING THE MEAN OF THE
C     LOWEST AND HIGHEST SUBSHELL ENERGIES TO BE THE SHELL ENERGY.
C     ADPALI(PQ,KN) IS IN KEV,  PQ IS THE EFFECTIVE NUCLEAR
C     CHARGE (IN UNITS OF ELECTRONIC CHARGE) AND KN IS THE PRINCIPAL
C     QUANTUM NUMBER.
C*******************************************************************
C
      IMPLICIT REAL*8 (A-H,O-Z)
      IMPLICIT INTEGER (I-N)
C
      ZALPHA = .007298
C
C
      ZKN = DBLE(KN)
C
      ADCPALI = .0136 * ( (PQ / ZKN)**2 ) *
     &     (1. + ((ZALPHA*PQ)**2) * (0.5 - 1./(4.*ZKN)) / ZKN)
      !YuP[2019-10-11] corrected bug: ADPALI --> ADCPALI
C
C
      RETURN
      END 
